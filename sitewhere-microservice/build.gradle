description = 'SiteWhere Common Microservice Library'

dependencies {
	compile project(':sitewhere-core')
	
	// Quarkus dependencies.
    compile enforcedPlatform('io.quarkus:quarkus-bom:0.26.1')
    compile 'io.quarkus:quarkus-kubernetes-client'
	
	// GRPC dependencies.
	compile project(':sitewhere-grpc-client')
	
	// GRPC server components.
	compile group: 'io.grpc', name: 'grpc-netty', version: "${rootProject.ext['grpc.version']}"
    
    // Google Analytics
	compile 'com.brsanthu:google-analytics-java:2.0.0'
	
	// Graal runtime support.
	compile group: 'com.oracle.substratevm', name: 'svm', version: '19.2.1'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

// Publish jar.	
apply plugin: 'maven-publish'
publishing {
	publications {
		mavenJava(MavenPublication) {
		    from components.java
            artifact sourcesJar
            artifact javadocJar
			pom {
			    name = 'SiteWhere Microservice Components'
			    description = 'SiteWhere Microservice Components Library'
			    url = 'http://sitewhere.io'
			    licenses {
			        license {
			            name = 'CPAL-1.0'
			            url = 'https://opensource.org/licenses/CPAL-1.0'
			        }
			    }
                developers {
                    developer {
                        id = 'admin'
                        name = 'SiteWhere Admin'
                        email = 'admin@sitewhere.com'
                    }
                }
                scm {
                    connection = 'https://github.com/sitewhere/sitewhere-microservice-core.git'
                    url = 'https://github.com/sitewhere/sitewhere-microservice-core'
                }
			}	
		}
	}
    repositories {
        maven {
            url project.nexusUrl
            credentials {
                username project.nexusUsername
                password project.nexusPassword
            }
        }
    }
}

if (project.hasProperty("signing.keyId")) {
	apply plugin: 'signing'
	signing {
	    sign publishing.publications.mavenJava
	}
}