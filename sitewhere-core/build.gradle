description = 'SiteWhere Core Library'

dependencies {
    compile project(':sitewhere-core-api')
    
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.9'
    compile group: 'org.apache.commons', name: 'commons-collections4', version: '4.4'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
	
	// Date/time conversions.
    compile group: 'joda-time', name: 'joda-time', version:'2.9.1'
	
	// Jackson JSON processing.
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${rootProject.ext['jackson.version']}"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "${rootProject.ext['jackson.version']}"
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: "${rootProject.ext['jackson.version']}"
          
    // Metrics support
    compile group: 'io.prometheus', name: 'simpleclient', version:'0.6.0'
    compile group: 'io.prometheus', name: 'simpleclient_httpserver', version:'0.6.0'
    
    // Retry logic.
    compile "com.evanlennick:retry4j:0.15.0"
    
    // Geolocation classes made available to scripts.
	compile group: 'com.vividsolutions', name: 'jts', version:'1.13'
}

license {
	exclude "**/core/Base58*"
}

processResources {
    from(sourceSets.main.resources.srcDirs) {
        include '**/*.properties'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [
            'build.timestamp': new java.text.SimpleDateFormat('yyyyMMddHHmmss').format(new Date()),
            'version.identifier' : project.version,
            'git.revision': rootProject.ext['git.revision'],
            'git.revision.abbrev': rootProject.ext['git.revision.abbrev']
        ])
    }
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}

// Publish jar.	
apply plugin: 'maven-publish'
publishing {
	publications {
		mavenJava(MavenPublication) {
		    from components.java
            artifact sourcesJar
            artifact javadocJar
			pom {
			    name = 'SiteWhere Core'
			    description = 'SiteWhere Core Runtime Components'
			    url = 'http://sitewhere.io'
			    licenses {
			        license {
			            name = 'CPAL-1.0'
			            url = 'https://opensource.org/licenses/CPAL-1.0'
			        }
			    }
                developers {
                    developer {
                        id = 'admin'
                        name = 'SiteWhere Admin'
                        email = 'admin@sitewhere.com'
                    }
                }
                scm {
                    connection = 'https://github.com/sitewhere/sitewhere-microservice-core.git'
                    url = 'https://github.com/sitewhere/sitewhere-microservice-core'
                }
			}	
		}
	}
    repositories {
        maven {
            url project.nexusUrl
            credentials {
                username project.nexusUsername
                password project.nexusPassword
            }
        }
    }
}

apply plugin: 'signing'
signing {
    sign publishing.publications.mavenJava
}

